
BuildProgramPage {
        wf *io.WriteFile
        wf:Content = *ProgramPage
        wf:Name = "program.html"
        : = wf:Ready
}

ProgramFigures {
	f *Fork
	f:ScanPrint = fig1:
	f:Telescope = fig2:
	f:Expanded = fig3:
	f:Flattened = fig4:
	f: = :

	fig1 *FigurePngSvgRight
	fig1:Image = "scanprint"
	fig1:Width = "400px"
	fig1:Caption = `*`
	fig1:Right = `
	<pre class="escher">
	main {
		scanln os.Scanln
		println os.Println
		scanln:Read = println:Write
	}
	</pre>
	`

	fig2 *FigurePngSvg
	fig2:Image = "telescope"
	fig2:Width = "600px"
	fig2:Caption = ``

	fig3 *FigurePngSvg
	fig3:Image = "expanded"
	fig3:Width = "500px"
	fig3:Caption = ``

	fig4 *FigurePngSvg
	fig4:Image = "flattened"
	fig4:Width = "500px"
	fig4:Caption = ``
}

ProgramPage {
	h *Html
	h: = :
	h:Title = "Programs and materialization"
	h:Body = t:

	t *text.QuickForm
	t:Data = *ProgramFigures
	t:Form = `

<h1>Programs and materialization</h1>

<p>
Materialization is the process of creating and executing a system of
interconnected reflexes, wherein the system is specified by a circuit
called a program.

<p>Materialization takes as input an index (of circuit programs and
other reflex materializers) and a circuit program to materialize.
The result of materialization is the creation and execution of the
system of reflexes described by the circuit program, as well as a
residual value.

<h2>Circuit programming</h2>

<p>A program circuit describes a system of interconnected reflexes.

<p>Each circuit gate corresponds to a reflex. The gate value 
describes the type of reflex that is to be materialized. While the
gate name is merely an identifier, unique to the program circuit,
used mainly to enable the specification of the reflex-to-reflex links.
The relation between gate values and materialized reflexes is explained later.

<p>In a valid program circuit, a gate whose name is the empty
string must not be defined. The empty-string gate is reserved for
the <em>super gate</em>, which (detailed later) designates an “enclosing” circuit.

<p>The circuit program's links connect pairs of reflex-valve endpoints.
Links are allowed either between two gates defined within the circuit,
or between a defined gate and the super gate.

<p>Links connected to the super gate are endpoints whose eventual connection
to a reflex is defered to the enclosing circuit.

<p>Circuit programs that have no links to the super gate are called <em>closed</em>
circuits, as they describe complete materializable systems on their own.

<p>On the other hand, circuit programs that have links to the super gate
can only be materialized as reflexes within higher-level enclosing circuits.

<h3>Gate value interpretation</h3>

<p>Circuit programs recognize the following types of gate values:
<ul>
<li>Integer, float, complex, string, or a non-directive circuit, or</li>
<li><a href="meaning.html#directives">Directive circuit</a>
</ul>

<h4>Noun reflexes</h4>

<p>Gate values in the first group (integer, float, complex, string or a non-directive circuit) 
will result in the materialization of a “noun” reflex, whose noun value is the
gate value.

<p>A noun reflex is a generic built-in reflex type which, after materialization,
emits its corresponding gate value to each one of its connected valves.
If no valves are connected, the noun reflex leaves the gate value as its 
residue. Otherwise, it leaves no residue.

<h4>Expanding directives</h4>

<p>When the gate value is a <a href="meaning.html#directives">directive</a>,
materialization proceeds as follows:

<ul>
<li>First, the runtime looks up the “target” value, which resides in the index
at the address specified in the directive.</li>
<li>Second,
<ul>
<li>If the directive verb is <code>@</code>, the gate is materialized as a noun gate
emitting the target value.</li>
<li>If the directive verb is <code>*</code>, the target value is substituted as the gate value,
and the materialization process described in this section is repeated now with the target
value as the gate value.
</ul>
</li>
</ul>

<h3>Circuit residue</h4>

<p>As pointed out in the <a href="reflex.html">section on reflexes</a>, every reflex
can leave a residue value as a result of being materialized, or the Go value <code>nil</code>
which indicates leaving no residue.

<p>Circuit programs are no different than reflexes (in fact they describe higher-order reflexes themselves)
in that they leave a residue value as well.

<p>The residue of materializing a circuit program is the same circuit,
wherein each gate value is replaced by the residue of materializing that gate.
Gate corresponding to reflexes that leave no residue are not present
in the residue circuit.

<p>If no gates leave any residue, the circuit program itself leaves no residue.

<h2>Example with an illustration</h2>

<p>Consider, for instance, the following index:

<pre>
{
	Database {
		cache Cache
		left Shard
		right Shard
		link Link

		cache:Web = :Web
		left:Cache = cache:Left
		right:Cache = cache:Right
		left:Backup = link:Left
		right:Backup = link:Right
		link: = :Backup
	}
	App {
		web Web
		left Database
		right Database
		backup Backup

		left:Web = web:Left
		right:Web = web:Right
		left:Backup = backup:Left
		right:Backup = backup:Right
	}
	Web …
	Cache …
	Shard …
	Link …
}
</pre>

<p>Here <code>App</code> and <code>Database</code> are program circuits and their symbolic
representation is shown below.
Whereas assume that <code>Web</code>, <code>Cache</code>, <code>Shard</code> and <code>Link</code>
(whose definition is not given in this listing) are reflex materializers.

{{.Gate.Telescope}}

If we materialize the program circuit <code>App</code> with respect to the index given above
(i.e. directive addresses will resolve with respect to that index), 
we are going to get the following residue:

<pre>
{
	web WebResidue
	bkp BackupResidue
	db1 {
		shard1 Shard1Residue
		shard2 Shard2Residue
		cache CacheResidue
		link LinkResidue

		cache:Web = :Web
		left:Cache = cache:Left
		right:Cache = cache:Right
		left:Backup = link:Left
		right:Backup = link:Right
		link: = :Backup
	}
	db2 {
		shard1 Shard1Residue
		shard2 Shard2Residue
		cache CacheResidue
		link LinkResidue

		cache:Web = :Web
		left:Cache = cache:Left
		right:Cache = cache:Right
		left:Backup = link:Left
		right:Backup = link:Right
		link: = :Backup
	}
	left:Web = web:Left
	right:Web = web:Right
	left:Backup = backup:Left
	right:Backup = backup:Right
}
</pre>

<p>Where <code>WebResidue</code>, <code>BackupResidue</code>, <code>Shard1Residue</code>, etc. are
merely placeholders here for whatever the actual residue values of the respetive reflexes are.
Visually the program residue could be represented as:

{{.Gate.Expanded}}

<p>The actual executed system of interconnected reflexes which results from the materialization
can be visualized like so:

{{.Gate.Flattened}}

<p>This illustration does not correspond to a circuit data structure anywhere in the runtime.
It is merely an illustration of the executed reflexes and their runtime connections.

<h2>Three ways to invoke materialization</h2>

<p>One can materialize (i.e. execute) a program circuit given an index from three different places:
from Go, from another program circuit (i.e. from Escher) and from the POSIX shell.

<h3>Materializing from Go</h3>

<p>Package <code>be</code> provides the materialization method:

<pre>
func MaterializeSystem(program interface{}, index, barrier Circuit) (residue interface{})
</pre>

<p>Argument <code>program</code> contains the program circuit, of Go type <code>Circuit</code>,
that is to be materialized. Incidentally, the value of <code>program</code> can be any value
recognized as a gate value in a circuit program as described earlier. Often one will pass a directive 
circuit as program.

<p>Argument <code>index</code> holds the materialization index, relative to which
directive addresses are interpreted.

<p>The last argument, <code>barrier</code>, is to be set to <code>nil</code>.

<p>The function returns the residue of the materialization process. 

<h3>Materializing from Escher</h3>

<p>One can recursively materialize circuits programs from within other 
circuit programs. This is accomplished using the built-in reflex <code>escher.Materialize</code>
which is described in the <a href="basis-escher.html">materialization faculty section</a>.

<h3>Materializing from POSIX</h3>

<p>The Escher executable, which is explained in detail in the <a href="runtime.html">runtime section</a>,
will materialize a directive from the command-line:

<pre>
% escher -src /src/app *app.Main
</pre>

`
}
