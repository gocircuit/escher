
BuildMeaningPage {
        wf *io.WriteFile
        wf:Content = *MeaningPage
        wf:Name = "meaning.html"
        : = wf:Ready
}

MeaningPage {
	h *Html
	h: = :
	h:Title = "Circuit interpretations"
	h:Body = t:

	f *Fork
	f:Ticker = tkr:

	tkr *FigureJpg
	tkr:Image = "tkr"
	tkr:Width = "500px"
	tkr:Caption = ``

	t *text.QuickForm
	t:Data = f:
	t:Form = `

<h1>Interpretations of circuits</h1>

<p>A circuit in Escher is a generic data abstraction much like a structure is in JavaScript or Go.
When thinking and programming in Escher, circuits will have a meaning
dependent on context and/or content. A circuit interpretation will usually utilize a subset of
the representational freedoms of a circuit, and will have an intuitive graphical representation.

<p>Here we introduce four basic circuit interpretations—or specializations, if you will—that
will also serve us as a vocabulary when discussing Escher in following chapters.

<h2 id="series">Series</h2>

<p>Circuit gate names, recall, can be integers or strings. If a circuit has no links and the gate
names are the integers 0, 1, …, K (for some integer K), we call the circuit a <em>series</em>.
Series are equivalent to arrays and/or slices in other programming languages, like C and Go.
Gate names are slice indices; gate values are slice element values.

<p>For instance, the circuit
<pre>
Singer {
	0 "Dolly"
	1 "Rebecca"
	2 "Parton"
}
</pre>
is analogous (in meaning) to the Go slice:
<pre>
var Singer = []interface{}{
	"Dolly",
	"Rebecca",
	"Parton",
}
</pre>

<p>Since series will be frequently input by the programmer, as explained in
the <a href="syntax.html#series">syntax section</a>, we have dedicated a shorthand
syntax for series circuits that omits the gate names:

<pre>
Singer {
	"Dolly"
	"Rebecca"
	"Parton"
}
</pre>

<h2 id="indices">Indices</h2>

<p>More generally than series, when a circuit has no links we call it an <em>index</em> and
we view it as a map from integers and/or strings to anything else. In this respect an index
is akin in purpose to structures, dictionaries, hash tables and maps in other languages.

<p>The gate values of index circuits are analogously called <em>children</em> and they can
be of primitive types (integers, floats, etc.) as well as recursively they can be other circuits
or indices.

<pre>
Tree {
	Trunk {
		Branches {
			"Johnny"
			"Katie"
		}
	}
	Root {
		Tentacles {
			"Grandpa"
			"Grandma"
		}
	}
}
</pre>

<p>Such recursive structures of indices, or just indices for short, serve the same purpose as
file-systems, namespaces, trees and others: To organize their internal and leaf values in a
hierarchical manner, so that each node (internal or leaf) is identifiable by a unique
path which we shall call <em>address</em> of a value relative to a given index.

<p>For instance, the address of <code>"Grandma"</code> relative to the index
<code>Tree</code> would be 

<pre>
{
	Root
	Tentacles
	1
}</pre>

(Note that addresses are represented by series circuits.)

<h2 id="sentences">Sentences</h2>

<p><em>Sentences</em> are a key building block of circuit programs (described next and in following sections).
A sentence is a pair of a string-valued <em>verb</em> and a target address.
Sentences are represented as a single circuit, wherein the empty-string gate holds the verb,
while the number gates hold the components of the address. For instance,

<pre>
{
	"" "*"
	0 Root
	1 Tentacles
	2 1
}
</pre>

<p>This circuit holds the verb value <code>"*"</code> and the address whose components
are <code>Root</code>, <code>Tentacles</code> and <code>1</code>, in that order.
There are only two types of verbs, signified by the verb values <code>"*"</code>
and <code>"@"</code>, whose meaning is explained in later sections. 
We call these verbs <em>materialize</em> and <em>recall</em>, respectively, while their 
single-characters values, <code>"*"</code> and <code>"@"</code>, are a design
choice of expediency.

<p>Due to the ubiquitous use of sentences in circuit programs, sentences can be written
using the <a href="syntax.html">dedicated syntactic sugar</a>:

<pre>
*Root.Tentacles.1
</pre>

<h2 id="programs">Programs</h2>

<p>Programs are circuits that describe executable systems. Their
meaning and use is explained in a <a href="programs.html">dedicated section</a>.
Here we describe their circuit structure.

<p>The gates of program circuits ultimately represent independently executing services,
which are interconnected according to the link pattern of the circuit.

<p>Gate values designate the processing logic—i.e. they codify the service type—while
gate names are used solely as identifiers, needed in the description of the circuit links.

<p>Gate values can be of any kind: integer, float, complex, string
or circuit. Often gate values will be sentence circuits, in which case they can be
written using the abbreviated syntax described earlier.

<p>Circuit links are allowed only between gate names,
defined within the circuit or the empty-string gate name.

<p>The empty-string gate name represents an implicit 
“enclosing” or “parent” circuit. In particular, program 
circuits are not allowed to define a gate with the empty-string
name.

<p>Links whose endpoints are connected to the same 
gate name are allowed, as long as they connect into different
valve names.

<p>Here is an example of a valid program circuit:

<pre>
{
	tkr *time.Ticker
	sum *math.Sum
	interval 1e9

	tkr:Duration = interval:
	tkr: = sum:Sum

	sum:X = :Phase
	sum:Y = *Show
}
</pre>

<p>And the corresponding symbolism:

{{.Gate.Ticker}}

`
}
